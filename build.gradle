plugins {
    id "java"
    id "idea"
    id "org.springframework.boot" version "2.3.0.RELEASE"
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id "com.commercehub.gradle.plugin.avro-base" version "0.9.1"
    id "com.google.protobuf" version "0.8.13"
}

apply from: 'libraries.gradle'

sourceCompatibility = 1.11
targetCompatibility = 1.11

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-logging"

    implementation "com.google.code.gson:gson"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.commons:commons-lang3"

    implementation "javax.xml.bind:jaxb-api"
    implementation "org.glassfish.jaxb:jaxb-runtime"
    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"
    implementation "com.jayway.jsonpath:json-path"
    implementation "org.apache.avro:avro:${avroVersion}"
    implementation "de.undercouch:bson4jackson:${bsonVersion}"
    implementation "org.msgpack:msgpack:${msgpackVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "org.msgpack:jackson-dataformat-msgpack:${jacksonMsgpackVersion}"

    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

task generateAvro(type: com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask) {
    source("src/main/resources/data/schema.avsc")
    outputDir = file("${project.buildDir}/generated/source/avro")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java', "${project.buildDir}/generated/source/proto", "${project.buildDir}/generated/source/avro"
        }
    }
}

compileJava.dependsOn(generateAvro)

idea {
    module {
        sourceDirs += [
                file("${project.buildDir}/generated/source/avro"),
                file("${project.buildDir}/generated/source/proto")
        ]
    }
}